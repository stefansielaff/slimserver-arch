# Maintainer: Stefan Sielaff <aur AT stefan-sielaff DOT de>

pkgname=logitechmediaserver
pkgver=7.9.0
pkgrel=1
pkgdesc='Streaming audio server'
arch=('i686' 'x86_64' 'arm')
url='https://github.com/stefansielaff/slimserver'
license=('GPL' 'custom')
depends=('perl<5.21')
makedepends=('nasm' 'yasm' 'rsync' 'gd' 'zlib' 'subversion')
[[ $CARCH == "x86_64" ]] && optdepends=(lib32-{glibc,gcc-libs}': transcoding on 64-bit systems')
install=install
source=('service')
sha256sums=('41718cf74b2f9b542089e7bd238fb974d049a6132e8d3e0ed598d6689e5d16c2')

prepare() {
	cd "${srcdir}"
	msg "Connecting to GitHub..."
	svn export --force ${url}-vendor/trunk/CPAN slimserver-vendor/CPAN
	svn export --force ${url}/trunk slimserver
	msg "Sources fetched or server timeout"
	cd "${srcdir}/slimserver"
	case $CARCH in
	    x86_64) rm -rf Bin/{arm,armhf}-linux ;;
	    i686) rm -rf Bin/{arm,armhf}-linux ;;
	    arm*) rm -rf Bin/{i386,x86_64}-linux ;;
	esac
}

build() {
	cd "${srcdir}/slimserver-vendor/CPAN"
	sh buildme.sh -t
}

package() {
	cd "${srcdir}/slimserver"
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../slimserver-vendor/CPAN/build/5.*/lib/*/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
