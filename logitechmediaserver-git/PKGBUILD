# Maintainer: Stefan Sielaff <aur AT stefan-sielaff DOT de>

pkgname=logitechmediaserver-git
pkgver=7.9.0
pkgrel=1
pkgdesc='Streaming audio server'
arch=('i686' 'x86_64' 'arm')
url='https://github.com/Logitech/slimserver'
license=('GPL' 'custom')
depends=('perl<5.21')
makedepends=('nasm' 'yasm' 'rsync' 'gd' 'zlib' 'subversion')
[[ $CARCH == "x86_64" ]] && optdepends=(lib32-{glibc,gcc-libs}': transcoding on 64-bit systems')
install=install
source=("slimserver::svn+https://github.com/Logitech/slimserver/trunk"
        "slimserver-vendor::svn+https://github.com/Logitech/slimserver-vendor/trunk"
        'service')
sha256sums=('SKIP'
            'SKIP'
            'f5c64f2a066914dbab9a1dd4a8ec33895645a72bde3bdbeb83c49e4624a997cb')

prepare() {
	cd "${srcdir}/slimserver-vendor"
	rm -r .svn .makepkg
	cd "${srcdir}/slimserver"
	rm -r .svn .makepkg
	rm -r CPAN/arch
	rm -r Bin/MSWin32-x86-multi-thread	
	rm -r Bin/darwin
	rm -r Bin/i386-freebsd-64int
	rm -r Bin/{powerpc,sparc}-linux
	case $CARCH in
	    x86_64) rm -r Bin/{arm,armhf}-linux ;;
	    i686) rm -r Bin/{arm,armhf}-linux ;;
	    arm*) rm -r Bin/i386-linux ;;
	esac
}

build() {
	cd "${srcdir}/slimserver-vendor/CPAN"
	sh buildme.sh -t
}

package() {
	cd "${srcdir}/slimserver"
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../slimserver-vendor/CPAN/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
