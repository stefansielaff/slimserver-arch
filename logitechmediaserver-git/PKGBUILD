# Maintainer: Stefan Sielaff <aur AT stefan-sielaff DOT de>

pkgname=logitechmediaserver-git
pkgver=7.9.0
pkgrel=2
pkgdesc='Streaming audio server'
arch=('i686' 'x86_64' 'arm')
url='https://github.com/stefansielaff/slimserver'
license=('GPL' 'custom')
depends=('perl<5.21')
makedepends=('nasm' 'yasm' 'rsync' 'gd' 'zlib' 'subversion')
[[ $CARCH == "x86_64" ]] && optdepends=(lib32-{glibc,gcc-libs}': transcoding on 64-bit systems')
install=install
source=('service')
sha256sums=('f5c64f2a066914dbab9a1dd4a8ec33895645a72bde3bdbeb83c49e4624a997cb')

prepare() {
	cd "${srcdir}"
	msg "Connecting to GitHub..."
	svn export --force https://github.com/stefansielaff/slimserver-vendor/trunk/CPAN slimserver-vendor/CPAN
	svn export --force https://github.com/stefansielaff/slimserver/trunk slimserver
	msg "Sources fetched or server timeout"
	cd "${srcdir}/slimserver"
	case $CARCH in
	    x86_64) rm -r Bin/{arm,armhf}-linux ;;
	    i686) rm -r Bin/{arm,armhf}-linux ;;
	    arm*) rm -r Bin/i386-linux ;;
	esac
}

build() {
	cd "${srcdir}/slimserver-vendor/CPAN"
	sh buildme.sh -t
}

package() {
	cd "${srcdir}/slimserver"
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../slimserver-vendor/CPAN/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
